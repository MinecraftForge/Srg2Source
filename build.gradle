buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "eclipse"
            url = "https://repo.eclipse.org/content/groups/eclipse/"
        }
    }
}

plugins {
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'net.minecraftforge'
archivesBaseName = 'srg2source'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.6'

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

configurations {
    deployerJars
}

dependencies {
    testCompile "junit:junit:4.12"

    compile 'org.ow2.asm:asm-debug-all:5.0.3' // Debug version, so we can have generics!
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'net.sf.jopt-simple:jopt-simple:4.6' // easy CLI parsing
    //compile 'org.eclipse.jgit:org.eclipse.jgit:3.2.0.201312181205-r' // git stuff? may not be used yet

    // necessary eclipse AST stuff
    compile 'org.eclipse.jdt:org.eclipse.jdt.core:3.14.0'

    // Potential Eclipse AST replacement (not used yet)
    //compile 'fr.inria.gforge.spoon:spoon-core:5.1.0'
}

repositories {
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "eclipse"
        url = "https://repo.eclipse.org/content/groups/eclipse"
    }
    mavenCentral()
}

jar {
    exclude 'data/**'
    manifest {
        attributes 'version': project.version
        attributes 'javaCompliance': project.targetCompatibility
        attributes 'group': project.group
        attributes 'Main-Class': 'net.minecraftforge.srg2source.ConsoleTool'
    }
}

shadowJar {
    classifier 'fatjar'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                from components.java
                artifact shadowJar
                name = 'Srg2Source'
                description = 'Srg2Source library for ForgeGradle'
                url = 'https://github.com/MinecraftForge/Srg2Source'
                scm {
                    url = 'https://github.com/MinecraftForge/Srg2Source'
                    connection = 'scm:git:git://github.com/MinecraftForge/Srg2Source.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/Srg2Source.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/Srg2Source/issues'
                }

                licenses {
                    license {
                        name = 'The BSD 3-Clause License'
                        url = 'http://opensource.org/licenses/BSD-3-Clause'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'agaricusb'
                        name = 'Agaricusb'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                    developer {
                        id = 'AbrarSyed'
                        name = 'Abrar Syed'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('mavenPassword')) {
                credentials {
                    username = project.properties.mavenUser
                    password = project.properties.mavenPassword
                }
                url 'http://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
