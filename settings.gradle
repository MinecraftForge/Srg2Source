pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Main
            library('jopt-simple', 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3')
            library('securemodules', 'net.minecraftforge:securemodules:2.2.21')
            library('srgutils', 'net.minecraftforge:srgutils:0.5.11')
            library('nulls', 'org.jetbrains:annotations:26.0.2') // Got to have our null annotations!

            library('eclipse-jdt',       'org.eclipse.jdt:org.eclipse.jdt.core:3.40.0')
            library('eclipse-runtime',   'org.eclipse.platform:org.eclipse.core.runtime:3.32.0')
            library('eclipse-resources', 'org.eclipse.platform:org.eclipse.core.resources:3.22.0')
            library('eclipse-jobs',      'org.eclipse.platform:org.eclipse.core.jobs:3.15.400')
            library('eclipse-types',     'org.eclipse.platform:org.eclipse.core.contenttype:3.9.600')
            bundle('eclipse', ['eclipse-jdt', 'eclipse-runtime', 'eclipse-resources', 'eclipse-jobs', 'eclipse-types'])

            // Test
            library('ml', 'net.minecraftforge:modlauncher:10.1.4')
            library('jimfs', 'com.google.jimfs:jimfs:1.3.0')

            version('junit', '5.12.0')
            library('junit-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            library('junit-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
            library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.12.0')
            bundle('junit-runtime', ['junit-engine', 'junit-platform-launcher'])

            version('asm', '9.7.1')
            library('asm',      'org.ow2.asm', 'asm'     ).versionRef('asm')
            library('asm-tree', 'org.ow2.asm', 'asm-tree').versionRef('asm')
            bundle('asm', ['asm', 'asm-tree'])

            version('powermock', '2.0.9')
            library('powermock-core', 'org.powermock', 'powermock-core').versionRef('powermock')
            library('powermock-reflect', 'org.powermock', 'powermock-reflect').versionRef('powermock')
            bundle('powermock', ['powermock-core', 'powermock-reflect'])
        }
    }
}

rootProject.name = 'Srg2Source'
